过程 智能引号操作()'★★★★★★
	变量 位置 为 整数型
	变量 左1 为 文本型
	变量 左2 为 文本型
	变量 右1 为 文本型
	变量 文本 为 文本型
	变量 左左 为 整数型
	变量 左右 为 整数型
	变量 右左 为 整数型
	变量 右右 为 整数型
	变量 无冒号文本 为 文本型
	无冒号文本 = "是成为其乃就能的作些称作之被有和及选择在会用不起么上下出入与和及跟叫并且可以要非来去离知只何啥意一百千万亿个只条根滩头排列堆为到拿以"
	位置 = 编辑框1.取光标位置()
	如果 位置 = -1 则
		如果 编辑框1.内容 = "" 则
			编辑框1.内容 = "“”"
			编辑框1.置光标位置(1)
		结束 如果
		退出
	结束 如果
	文本 = 编辑框1.内容
	左左 = 倒找文本(文本,"“",位置)
	左右 = 倒找文本(文本,"”",位置)
	右左 = 寻找文本(文本,"“",位置)
	右右 = 寻找文本(文本,"”",位置)
	如果 位置>1 则
		左2 = 取文本中间(文本,位置-2,1)
		左1 = 取文本中间(文本,位置-1,1)
	结束 如果
	如果 位置 < 取文本长度(文本) 则
		右1 = 取文本中间(文本,位置,1)
	结束 如果
	如果 编辑按钮.可视 = 真 且 起始选中 < 结束选中 则
		如果 寻找文本(取文本中间(文本, 起始选中, 结束选中-起始选中), "\n", 0) > -1 则
			添加多段引号(起始选中, 结束选中)
			退出
		结束 如果
		变量 左引号 为 整数型 = 0
		变量 右引号 为 整数型
		变量 是否有左引号 为 逻辑型
		变量 是否有右引号 为 逻辑型
		变量 左标点 为 整数型
		变量 右标点 为 整数型
		判断循环首 起始选中 < 结束选中 且 寻找文本(" 　\n\t", 取文本中间(文本, 起始选中, 1), 0) > -1
			起始选中 = 起始选中 + 1 '忽略左边空白符
		判断循环尾
		判断循环首 结束选中 > 0 且 起始选中 < 结束选中 且 寻找文本(" 　\n\t", 取文本中间(文本, 结束选中-1, 1), 0) > -1
			结束选中 = 结束选中 - 1 ' 忽略右边空白符
		判断循环尾
		如果 起始选中 = 结束选中 则
			冒出提示2("选中的文字空荡荡的,不能设置为对话")
			退出
		结束 如果
		如果 起始选中 > 0 且 取文本中间(文本, 起始选中-1, 1) = "“" 则'左1
			左引号 = 2
		否则如果 取文本中间(文本, 起始选中, 1) = "“" 则'右1
			左引号 = 1
		结束 如果
		如果 结束选中 < 取文本长度(文本) 且 取文本中间(文本, 结束选中, 1) = "”" 则'右1
			右引号 = 1
		否则如果 取文本中间(文本, 结束选中-1, 1) = "”" 则'左1
			右引号 = 2
		结束 如果
		如果 寻找文本(取文本中间(文本, 起始选中+1, 结束选中-起始选中-1), "“", 0) > -1 则
			是否有左引号 = 真
		结束 如果
		如果 寻找文本(取文本中间(文本, 起始选中, 结束选中-起始选中-1), "”", 0) > -1 则
			是否有右引号 = 真
		结束 如果
		如果 起始选中 > 0 且 是否为中文(取文本中间(文本, 起始选中-1, 1)) = 假 则
			左标点 = 1
		否则如果 是否为中文(取文本中间(文本, 起始选中, 1)) = 假 则
			左标点 = 2
			如果 结束选中 > 起始选中+1 则
				变量 左右2 为 文本型 = 取文本中间(文本, 起始选中+1, 1)
				如果 是否为中文(左右2) = 真 则
					左标点 = 3
				结束 如果
			结束 如果
		结束 如果
		如果 是否为中文(取文本中间(文本, 结束选中-1, 1)) = 假 则
			右标点 = 1
		否则如果 结束选中 < 取文本长度(文本) 且 是否为中文(取文本中间(文本, 结束选中, 1)) = 假 则
			右标点 = 2
		结束 如果
		如果 是否有左引号 = 真 且 是否有右引号 = 真 则
			冒出提示("注意：里面有个对话,请注意修改")
		否则如果 是否有左引号 = 真 或 是否有右引号 = 真 则
			冒出提示("注意：里面有半句对话,请注意修改")
		结束 如果
		变量 引号位置 为 整数型
		变量 引号位置2 为 整数型
		如果 左引号 > 0 且 右引号 > 0 则
			主动改变 = 假
			引号位置 = 起始选中'避免内容改变后起始选中 = 0
			编辑框1.删除文本(结束选中+1-右引号, 结束选中+1-右引号+1)
			编辑框1.删除文本(起始选中+1-左引号, 起始选中+1-左引号+1)
		否则如果 左引号 > 0 则
			编辑框1.插入文本(结束选中, "”")
			引号位置 = 结束选中 + 1
			引号位置2 = 寻找文本(文本, "“", 引号位置)
			引号位置 = 寻找文本(文本, "”", 引号位置+1)
			如果 引号位置 > 引号位置2 则
				编辑框1.删除文本(引号位置, 引号位置 + 1)'补充后多余的右引号
			结束 如果
		否则如果 右引号 > 0 则
			编辑框1.插入文本(起始选中, "“")
			引号位置 = 倒找文本(文本, "“", 起始选中-1)
			引号位置2 = 倒找文本(文本, "”", 起始选中)
			如果 引号位置 > -1 且 引号位置 > 引号位置2 则
				编辑框1.删除文本(引号位置, 引号位置 + 1)
			结束 如果
		否则
			主动改变 = 假
			如果 起始选中 > 0 则
				左1 = 取文本中间(文本, 起始选中-1, 1)
			结束 如果
			编辑框1.插入文本(结束选中, "”")
			如果 左标点 = 3 则
				编辑框1.插入文本(起始选中+1, "“")
				退出
			否则
				编辑框1.插入文本(起始选中, "“")
			结束 如果
			
			如果 左标点 <> 1 且 右标点 <> 2 则
				编辑框1.插入文本(起始选中, 取引号逗号冒号(文本, 起始选中, 真))
			结束 如果
		结束 如果
	否则如果 右1 = "”" 则
		如果 是否为中文(左1) = 真 则
			变量 位置1 为 整数型
			位置1 = 倒找文本(文本,"“",位置)
			如果 位置1 = -1 则 '补充前引号
				位置1 = 位置1 + 1
				判断循环首 是否为中文(取文本中间(文本,位置1,1)) = 假 且 位置1 < 位置
					位置1 = 位置1 + 1
				判断循环尾
				如果 位置1 < 位置 则
					编辑框1.插入文本(位置1,"“")
					编辑框1.置光标位置(位置+2)
					冒出提示("前引号被吃了？已经补了一个")
				否则
					冒出提示("前方无适当位置可以添加前引号")
				结束 如果
			否则如果 位置1 > 0 且 是否为中文(取文本中间(文本,位置1-1,1)) = 真 则'不需要后面的标点
				编辑框1.置光标位置(位置+1)
			否则如果 倒找文本(文本,"、“",位置) = 位置1-1 则
				编辑框1.置光标位置(位置+1)
			否则'需要后面的标点
				变量 位置2 为 整数型
				位置2 = 倒找文本(文本,"”",位置-1)
				如果 位置1 > 0 且 位置2 < 位置1 且 是否为中文(取文本中间(文本,位置1-1,1)) = 真 则
					右移添加("")
				否则
					右移添加(取标点(-1))
				结束 如果
			结束 如果
		否则如果 左1 = "“" 则
			如果 左2 = "，" 则
				主动改变 = 假
				编辑框1.删除文本(位置-2,位置-1)
				编辑框1.插入文本(位置-2,"：")
			否则如果 左2 = "：" 则
				如果 位置 > 5 且 倒找文本(文本,"\n",位置) < 倒找文本(文本,"\n",位置-1) 则
					编辑框1.删除文本(位置-2,位置-1)
					编辑框1.插入文本(位置-2,"，")
				否则
					编辑框1.删除文本(位置-2,位置-1)
				结束 如果
			否则如果 左右 > 倒找文本(文本,"\n",位置) 则
				编辑框1.插入文本(位置-1,"，")
			否则
				编辑框1.插入文本(位置-2,"：")
			结束 如果
		否则
			右移添加("")
		结束 如果
	否则如果 左1 = "”" 则
		如果 是否为中文(左2) = 假 且 左2 <> "“" 则
			编辑框1.置光标位置(编辑框1.取光标位置()-1)
			是否添加符号 = 假
			编辑框1.删除文本(编辑框1.取光标位置()-1,编辑框1.取光标位置())
		否则
			编辑框1.置光标位置(编辑框1.取光标位置()-1)
			右移添加(取标点(-1))
		结束 如果
	否则如果 右1 = "“" 则
		如果 左1 = "：" 则
			如果 倒找文本(文本,"\n",位置) < 左右 则
				主动改变 = 假
				编辑框1.删除文本(位置-1,位置)
				编辑框1.插入文本(位置-1,"，")
			否则
				编辑框1.删除文本(位置-1,位置)
			结束 如果
		否则如果 左1 = "，" 则
			主动改变 = 假
			编辑框1.删除文本(位置-1,位置)
			编辑框1.插入文本(位置-1,"：")
		否则如果 倒找文本(文本,"\n",位置) < 左右 则
			编辑框1.插入文本(位置,"，")
		否则如果 左1 = " " 则
			编辑框1.置光标位置(位置+1)
		否则如果 左1 = 代码转字符(12288) 则
			编辑框1.置光标位置(位置+1)
		否则如果 左1 = "\n" 则
			编辑框1.置光标位置(位置+1)
		否则如果 倒找句首(位置) <= 倒找文本(文本,"”",位置)+2 则
			编辑框1.插入文本(位置,"，")
		否则
			编辑框1.插入文本(位置,"：")
		结束 如果
	否则如果 左1 = "“" 则
		判断 左2
			分支 "，"
				主动改变 = 假
				编辑框1.删除文本(位置-2,位置-1)
				编辑框1.插入文本(位置-2,"：")
			分支 "："
				编辑框1.删除文本(位置-2,位置-1)
			分支 " "
				编辑框1.置光标位置(位置+1)
			分支 代码转字符(12288)
				编辑框1.置光标位置(位置+1)
			分支 "\n"
				编辑框1.置光标位置(位置+1)
			分支 否则
				如果 是否为中文(左2) = 真 则
					如果 倒找文本(文本,"\n",位置) < 左右 则
						编辑框1.插入文本(位置-1,"，")
					否则
						编辑框1.插入文本(位置-1,"：")
					结束 如果
				结束 如果
		结束 判断
	否则如果 右1 = "’" 则
		变量 位置1 为 整数型
		位置1 = 倒找文本(文本,"‘",位置)
		如果 位置1 > 0 则
			如果 取文本中间(文本,位置1-1,1) = "，" 或 取文本中间(文本,位置1-1,1) = "：" 则
				编辑框1.插入文本(位置,取标点(-1))
			结束 如果
		结束 如果
		编辑框1.置光标位置(位置+1)
	否则如果 左左 <= 左右 且 右左 = -1 且 右右 > -1 则'插入前引号
		如果 是否为中文(左1) = 真 则
			如果 左右 = -1 则
				编辑框1.插入文本(位置,"：“")
			否则
				如果 左右 > 倒找文本(文本,"\n",位置) 则
					编辑框1.插入文本(位置,"，“")
				否则
					编辑框1.插入文本(位置,"：“")
				结束 如果
			结束 如果
		否则
			编辑框1.插入文本(位置,"“")
		结束 如果
	否则如果 左左 <= 左右 且 右左 > 右右 则'插入前引号
		如果 是否为中文(左1) = 真 则
			如果 左右 = -1 则
				编辑框1.插入文本(位置,"：“")
			否则
				如果 左右 > 倒找文本(文本,"\n",位置) 则
					编辑框1.插入文本(位置,"，“")
				否则
					编辑框1.插入文本(位置,"：“")
				结束 如果
			结束 如果
		否则
			编辑框1.插入文本(位置,"“")
		结束 如果
	否则如果 右1 = "：" 则
		变量 右2 为 文本型
		右2 = 取文本中间(文本,位置+1,1)
		如果 右2 = "“" 则
			编辑框1.删除文本(位置,位置+1)
		结束 如果
	否则如果 右1 = "，" 则
		变量 右2 为 文本型
		右2 = 取文本中间(文本,位置+1,1)
		如果 右2 = "“" 则
			主动改变 = 假
			编辑框1.删除文本(位置,位置+1)
			编辑框1.插入文本(位置,"“")
		否则如果 左左 > 左右 且 右左 <= 右右 则
			编辑框1.插入文本(位置,"”")
		结束 如果
	否则如果 左左 > 左右 且 右右 < 右左 且 右左 > 0 则'引号里面
		如果 寻找文本(无冒号文本,左1,0) <> -1 则'如果 左1 = "的" 则
			编辑框1.插入文本(编辑框1.取光标位置(),"‘")
			变量 寻找位置 为 整数型
			寻找位置 = 寻找文本(文本,"”",位置+1)
			编辑框1.插入文本(寻找位置+1,"’")
			编辑框1.置光标位置(寻找位置+2)
		否则
			编辑框1.插入文本(编辑框1.取光标位置(),"：‘")
			变量 寻找位置 为 整数型
			寻找位置 = 寻找文本(文本,"”",位置+2)
			编辑框1.插入文本(寻找位置+2,"’")
			编辑框1.置光标位置(寻找位置+2)
		结束 如果
	否则如果 左左 > 左右 且 右右 > 0 且 右左 < 0 则'引号里面
		如果 寻找文本(无冒号文本,左1,0) <> -1 则
			编辑框1.插入文本(编辑框1.取光标位置(),"‘")
			变量 寻找位置 为 整数型
			寻找位置 = 寻找文本(文本,"”",位置)
			编辑框1.插入文本(寻找位置+1,"’")
			编辑框1.置光标位置(寻找位置+2)
		否则
			编辑框1.插入文本(编辑框1.取光标位置(),"：‘")
			变量 寻找位置 为 整数型
			寻找位置 = 寻找文本(文本,"”",位置)
			编辑框1.插入文本(寻找位置+2,"’")
			编辑框1.置光标位置(寻找位置+2)
		结束 如果
	否则如果 左左 > 左右 且 右右 >= 右左 则'缺后引号
		如果 是否为中文(左1) = 真 则
			编辑框1.插入文本(位置,取标点(-1) & "”")
		否则
			编辑框1.插入文本(位置,"”")
		结束 如果
	否则如果 左左 <= 左右 且 右左 = -1 且 右右 <> -1 则'缺前引号
		右移添加("“")
	否则如果 左左 <= 左右 且 右左 = -1 且 右右 <> -1 且 右左 > 右右 则'缺前引号
		右移添加("“")
	否则如果 寻找文本(无冒号文本,左1,0) <> -1 则
		引号添加("")
	否则
		如果 是否为中文(左1) = 真 则
			变量 位置1 为 整数型
			变量 文本1 为 文本型
			变量 位置2 为 整数型
			位置1 = 左右 + 1
			位置2 = 倒找文本(文本,"\n",位置-1)
			文本1 = 取文本中间(文本,位置1,1)
			如果 位置1 > 位置2  且 位置1 > 0且 是否为中文(文本1) = 真 则
				引号添加("，")
			否则
				引号添加("：")
			结束 如果
		否则
			引号添加("")
		结束 如果
	结束 如果
	安全保存(4)
	容错处理首
		分支 否则
			引号添加("")
			冒出提示2("智能引号操作出错")
	容错处理尾
结束 过程